use crate::ast::*;
use crate::lexer::errors::LexError;
use crate::location::Location as LexLocation;
use crate::lexer::token::TokenKind;
grammar;

pub Algorithm: Algorithm = {
    <lines:FileLine*> => Algorithm::Body(lines)
};

pub FileLine: FileLine = {
         <labels: LabelsDeclaration?> <statements:Statements> "END_OF_FILE"=> { 
        match labels {
                        Some(labels) => FileLine::Line{labels, statements: statements},
                        None => FileLine::Line{labels: vec![], statements: statements}
                    }
                },
        <labels: LabelsDeclaration?> <statements:Statements> "NEW_LINE"=> { 
        match labels {
                        Some(labels) => FileLine::Line{labels, statements: statements},
                        None => FileLine::Line{labels: vec![], statements: statements}
                    }
                },
};

pub LabelsDeclaration: Vec<String> = {
    <labels: Labels> "..." => labels
};

#[inline]
Labels: Vec<String> = { // (1)
    <mut v:(<Identifier> ",")*> <e:Identifier?> => match e { // (2)
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

pub OneLineStatement: OneLineStatement = {
    CallSubProgram,
    UnconditionalJump,
    Loop,
    Predicate,
    <l_location:@L> "!" <r_location:@L> => OneLineStatement
        {
            l_location,
            r_location,
            node: OneLineStatementKind::Exit,
        },
    <l_location:@L> "RETURN" <r_location:@L> => OneLineStatement
        {
            l_location,
            r_location,
            node: OneLineStatementKind::Return
        }
};

pub CallSubProgram: OneLineStatement = {
    <l_location:@L> "SUB_PROGRAM" <sp_name: Identifier> "{" <args: Parameters> "}" <label_to: Identifier> <r_location:@L> => OneLineStatement
        {
            l_location,
            r_location,
            node: OneLineStatementKind::SubProgram
                {
                sp_name,
                args: args.into_iter().map(Box::new).collect(),
                label_to
                }
}
};

pub Predicate: OneLineStatement = {
    <l_location:@L> "PREDICATE" "{" <condition:ExpressionPrecedence8> "}" <l:Statements> "|" <r:Statements> <r_location:@L> => OneLineStatement
        {
            l_location,
            r_location,
            node: OneLineStatementKind::Predicate
                {
                condition,
                if_true: Box::new(l),
                if_false: Box::new(r)
                }
        }
};

pub Loop: OneLineStatement = {
    <l_location:@L> "LOOP" "{" <a:Expression> "," <b:Expression> "," <c:Expression>  "=>" <e: Expression> "}" <alpha: Identifier> <betta: Identifier> <r_location:@L> => OneLineStatement
        {
            l_location,
            r_location,
            node: OneLineStatementKind::Loop
                {
                initial_value: a,
                step: b,
                last_value_or_condition: c,
                iterator: e,
                label_until: alpha, 
                label_to: betta 
                }
        }

};

pub UnconditionalJump: OneLineStatement = {
    <l_location:@L> "@" <label: Identifier> <r_location:@L> => OneLineStatement
        {
            l_location,
            r_location,
            node: OneLineStatementKind::UnconditionalJump{label},
        }
};

pub SimpleStatement: SimpleStatement = {
    <l_location:@L> <lhs:Expression> "=" <rhs:Expression> <r_location:@L> => SimpleStatement
        {
            l_location,
            r_location,
            node: SimpleStatementKind::Assign{lhs, rhs},
        },
    <l_location:@L> <lhs:Expression> "<=>" <rhs:Expression> <r_location:@L> => SimpleStatement
        {
            l_location,
            r_location,
            node: SimpleStatementKind::Exchange{lhs, rhs},
        },
    <l_location:@L> <lhs:Expression> "=>" <rhs:Expression> <r_location:@L> => SimpleStatement
        {
            l_location,
            r_location,
            node: SimpleStatementKind::Send{lhs: rhs,rhs: lhs},
        },
    <l_location:@L> <expression: Expression> <r_location:@L> => SimpleStatement
        {
            l_location,
            r_location,
            node: SimpleStatementKind::Expression{expression},
        }
    };


pub Expression: Expression = {
    ExpressionPrecedence8,
    };

ExpressionPrecedence8: Expression = {
    <l_location:@L> <lhs:ExpressionPrecedence8> "OR" <rhs:ExpressionPrecedence7> <r_location:@L> => Expression
        {
            l_location,
            r_location,
            node: ExpressionKind::BinaryOp{op: BinaryOp::Or, lhs: Box::new(lhs), rhs: Box::new(rhs)},
        },

    ExpressionPrecedence7,
};

ExpressionPrecedence7: Expression = {
    <l_location:@L> <lhs:ExpressionPrecedence7> "AND" <rhs:ExpressionPrecedence6> <r_location:@L> => Expression
        {
            l_location,
            r_location,
            node: ExpressionKind::BinaryOp{op: BinaryOp::And, lhs: Box::new(lhs), rhs: Box::new(rhs)},
        },
    ExpressionPrecedence6,
};

ExpressionPrecedence6: Expression = {
    <l_location:@L> <lhs:ExpressionPrecedence6> "==" <rhs:ExpressionPrecedence5> <r_location:@L> => Expression
        {
            l_location,
            r_location,
            node: ExpressionKind::BinaryOp{op: BinaryOp::EQ, lhs: Box::new(lhs), rhs: Box::new(rhs)},
        },
    <l_location:@L> <lhs:ExpressionPrecedence6> "!=" <rhs:ExpressionPrecedence5> <r_location:@L> => Expression
        {
            l_location,
            r_location,
            node: ExpressionKind::BinaryOp{op: BinaryOp::NE, lhs: Box::new(lhs), rhs: Box::new(rhs)},
        },
    <l_location:@L> <lhs:ExpressionPrecedence6> "<" <rhs:ExpressionPrecedence5> <r_location:@L> => Expression
        {
            l_location,
            r_location,
            node: ExpressionKind::BinaryOp{op: BinaryOp::LT, lhs: Box::new(lhs), rhs: Box::new(rhs)},
        },
    ExpressionPrecedence5
}

// ArithmeticExpression
ExpressionPrecedence5: Expression = {
    <l_location:@L>  <lhs:ExpressionPrecedence5> "+" <rhs:ExpressionPrecedence4> <r_location:@L> =>  Expression
        {
            l_location,
            r_location,
            node: ExpressionKind::BinaryOp{op: BinaryOp::Sum, lhs: Box::new(lhs), rhs: Box::new(rhs)},
        },
    <l_location:@L> <lhs:ExpressionPrecedence5> "-" <rhs:ExpressionPrecedence4> <r_location:@L>=>  Expression
        {
            l_location,
            r_location,
            node: ExpressionKind::BinaryOp{op: BinaryOp::Sub, lhs: Box::new(lhs), rhs: Box::new(rhs)},
        },
    ExpressionPrecedence4
}

// FactorExpression
ExpressionPrecedence4: Expression = {
    <l_location:@L> <lhs:ExpressionPrecedence4> "*" <rhs:ExpressionPrecedence3> <r_location:@L> => Expression
        {
            l_location,
            r_location,
            node: ExpressionKind::BinaryOp{op: BinaryOp::Mul, lhs: Box::new(lhs), rhs: Box::new(rhs)},
        },
    <l_location:@L> <lhs:ExpressionPrecedence4> "/" <rhs:ExpressionPrecedence3> <r_location:@L> => Expression
        {
            l_location,
            r_location,
            node: ExpressionKind::BinaryOp{op: BinaryOp::Div, lhs: Box::new(lhs), rhs: Box::new(rhs)},
        },
    ExpressionPrecedence3
};

ExpressionPrecedence3: Expression = {
    ExpressionPrecedence2
}

ExpressionPrecedence2: Expression = {
    <l_location:@L> "NOT" <expression:ExpressionPrecedence2> <r_location:@L> => Expression 
        {
        l_location,
        r_location,
        node: ExpressionKind::UnaryOp{op: UnaryOp::Not, expr: Box::new(expression)},
        },
    <l_location:@L> "'" <expression:ExpressionPrecedence2> <r_location:@L> => Expression 
        {
        l_location,
        r_location,
        node: ExpressionKind::UnaryOp{op: UnaryOp::Dereference, expr: Box::new(expression)},
        },

    ExpressionPrecedence1
}

ExpressionPrecedence1: Expression = {
    LiteralExpression,
    List,
    FunctionCall,
    MultipleDereference,
    Variable,
    "(" <Expression> ")",

};

LiteralExpression: Expression = {
    BoolLiteral,
    IntLiteral,
    StringLiteral,
    FloatLiteral,
    NullLiteral
};

FunctionCall: Expression = {
    <l_location:@L> <function: "IDENTIFIER"> "{" <args: Parameters> "}" <r_location:@L> => Expression 
        {
        l_location,
        r_location,
        node: ExpressionKind::Call{function, args: args.into_iter().map(Box::new).collect()},
        }
}

MultipleDereference: Expression = {
        <l_location:@L>  "DEREF" "{" <expression:Expression>  "," <r: Expression> "}" <r_location:@L>  => Expression 
        {
        l_location,
        r_location,
        node: ExpressionKind::UnaryOp{op: UnaryOp::MultipleDereference(Box::new(r)), expr: Box::new(expression)},
        },
}
#[inline]
Comma<T>: Vec<T> = { // (1)
    <mut v:(<T> ",")*> <e:T?> => match e { // (2)
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

#[inline]
Semicolon<T>: Vec<T> = { // (1)
    <mut v:(<T> ";")*> <e:T?> => match e { // (2)
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};
#[inline]
Statements: Statements = {
    <statements: Semicolon<SimpleStatement>> => Statements::SimpleStatements(statements),
    <statement:OneLineStatement> => Statements::OneLineStatement(statement)
};

#[inline]
SimpleStatements = Semicolon<SimpleStatement>;

#[inline]
Parameters = Comma<Expression>;

#[inline]
Elements = Comma<Expression>;

Variable: Expression = {
   <l_location:@L> <name:"IDENTIFIER"> <r_location:@L> => Expression 
        {
        l_location,
        r_location,
        node: ExpressionKind::Var{name}
        }
}

List: Expression = {
    <l_location:@L> "[" <elements: Parameters> "]"<r_location:@L> => Expression 
        {
        l_location,
        r_location,
        node: ExpressionKind::List {
            elements: elements.into_iter().map(Box::new).collect()
        },
        }
};

NullLiteral: Expression = {
    <l_location:@L> <n:"NULL"> <r_location:@L> => Expression 
        {
        l_location,
        r_location,
        node: ExpressionKind::Null,
        }
};


IntLiteral: Expression = {
   <l_location:@L> <n:"INTEGER_LITERAL"> <r_location:@L> => Expression 
        {
        l_location,
        r_location,
        node: ExpressionKind::Int{value: n}
        }
};

FloatLiteral: Expression = {
   <l_location:@L> <n:"FLOAT_LITERAL"> <r_location:@L> => Expression 
        {
        l_location,
        r_location,
        node: ExpressionKind::Float{value: n},
        }
}

BoolLiteral: Expression = {
    <l_location:@L> "TRUE" <r_location:@L> => Expression 
        {
        l_location,
        r_location,
        node: ExpressionKind::Bool{value: true}
        },
    <l_location:@L> "FALSE" <r_location:@L> => Expression 
        {
        l_location,
        r_location,
        node: ExpressionKind::Bool{value: false}
        },
};

StringLiteral: Expression = {
   <l_location:@L> <s: "STRING_LITERAL"> <r_location:@L> => Expression {
    l_location,
    r_location,
    node: ExpressionKind::String{value: s}}
};

Identifier: String = {
    <i: "IDENTIFIER"> => i
};

extern {
    type Location = LexLocation;
    type Error = LexError;

    enum TokenKind {
    "IDENTIFIER" => TokenKind::Identifier (<String>),
    "INTEGER_LITERAL" => TokenKind::IntegerLiteral (<i64>),
    "FLOAT_LITERAL" => TokenKind::FloatLiteral (<f64>),
    "STRING_LITERAL" => TokenKind::StringLiteral (<String>),
    "NEW_LINE" => TokenKind::NewLine,
    "END_OF_FILE" => TokenKind::EndOfFile,
    "(" => TokenKind::LeftParenthesis,
        ")" => TokenKind::RightParenthesis,
        "[" => TokenKind::LeftSquareBracket,
        "]" => TokenKind::RightSquareBracket,
        ":" => TokenKind::Colon,
        "," => TokenKind::Comma,
        ";" => TokenKind::Semicolon,
        "*" => TokenKind::Multiply,
        "+" => TokenKind::Plus,
        "-" => TokenKind::Minus,
        "/" => TokenKind::Slash,
        "|" => TokenKind::VerticalBar, // '|'
        "&" => TokenKind::Ampersand,    // '&'
        "<" => TokenKind::LessThan,
        ">" => TokenKind::GreaterThan,
        "=" => TokenKind::Equal,
        "." => TokenKind::Dot,
        "%" => TokenKind::Percent,
        "{" => TokenKind::LeftCurlyBrace,
        "}" => TokenKind::RightCurlyBrace,
        "==" => TokenKind::EqualEqual,
        "!=" => TokenKind::NotEqual,
        "=<" => TokenKind::LessThanEqual,
        ">=" => TokenKind::GreaterThanEqual,
        ">>" => TokenKind::RightShift,
        "<<" => TokenKind::LeftShift,
        "//" => TokenKind::DoubleSlash,
        "=>" => TokenKind::Send,
        "..." => TokenKind::Ellipsis,
        "'" => TokenKind::Apostrophe,
        "<=>" => TokenKind::Exchange,
        "REPLACE" => TokenKind::Replace,
        "LOOP" => TokenKind::Loop,
        "PREDICATE" => TokenKind::Predicate,
        "SUB_PROGRAM" =>TokenKind::SubProgram,
        "@" => TokenKind::At,
        "!" => TokenKind::Bang,
        "RETURN" => TokenKind::Return,
        "FALSE" => TokenKind::False,
        "NULL" => TokenKind::Null,
        "TRUE" => TokenKind::True,
        "AND" => TokenKind::And,
        "DEL" => TokenKind::Del,
        "NOT" => TokenKind::Not,
        "OR" => TokenKind::Or,
        "LET" => TokenKind::Let,
        "CONST" => TokenKind::Const,
        "DEREF" => TokenKind::Deref,
    }
}