use crate::ast::*;
use crate::errors::LexError;
use crate::lexer::location::Location as LexLocation;
use crate::lexer::token::Token;
grammar;

pub Algorithm: Algorithm = {
    <lines:FileLine*> => Algorithm::Body{lines},
};

pub FileLine: FileLine = {
         <labels: LabelsDeclaration?> <statements:Statements> "END_OF_FILE"=> { 
        match labels {
                        Some(labels) => FileLine::Line{labels, statements: statements},
                        None => FileLine::Line{labels: vec![], statements: statements}
                    }
                },
        <labels: LabelsDeclaration?> <statements:Statements> "NEW_LINE"=> { 
        match labels {
                        Some(labels) => FileLine::Line{labels, statements: statements},
                        None => FileLine::Line{labels: vec![], statements: statements}
                    }
                },
};

pub LabelsDeclaration: Vec<String> = {
    <labels: Labels> "..." => labels
};

#[inline]
Labels: Vec<String> = { // (1)
    <mut v:(<Identifier> ",")*> <e:Identifier?> => match e { // (2)
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

pub OneLineStatement: OneLineStatement = {
    UnconditionalJump,
    Loop,
    Predicate,
    "!" => OneLineStatement::Exit
    
};

pub Predicate: OneLineStatement = {
    "PREDICATE" "{" <condition:ExpressionPrecedence8> "}" <l:Statements> "|" <r:Statements> => {
        OneLineStatement::Predicate{
            condition
          , if_true: Box::new(l)
          , if_false: Box::new(r)}
        }
};

pub Loop: OneLineStatement = {
    "LOOP" "{" <a:Expression> "," <b:Expression> "," <c:Expression> "}" <alpha: Identifier> <betta: Identifier> => OneLineStatement::Loop{
        initial_value: a
      , step: b
      , last_value: c
      , label_until: alpha, 
      label_to: betta }
};

pub UnconditionalJump: OneLineStatement = {
    "@" <label: Identifier> => OneLineStatement::UnconditionalJump{label},
};

pub SimpleStatement: SimpleStatement = {
    <lhs:Expression> "=" <rhs:Expression> => SimpleStatement::Assign{lhs, rhs},
    <lhs:Expression> "<=>" <rhs:Expression> => SimpleStatement::Exchange{lhs, rhs},
    <lhs:Expression> "=>" <rhs:Expression> => SimpleStatement::Send{lhs: rhs,rhs: lhs},
    <expression: Expression> => SimpleStatement::Expression{expression},
    };


pub Expression: Expression = {
    ExpressionPrecedence8,
    };

ExpressionPrecedence8: Expression = {
    <lhs:ExpressionPrecedence8> "OR" <rhs:ExpressionPrecedence7> => Expression::BinaryOp{op: BinaryOp::Or, lhs: Box::new(lhs), rhs: Box::new(rhs)},
    ExpressionPrecedence7,
};

ExpressionPrecedence7: Expression = {
    <lhs:ExpressionPrecedence7> "AND" <rhs:ExpressionPrecedence6> => Expression::BinaryOp{op: BinaryOp::And, lhs: Box::new(lhs), rhs: Box::new(rhs)},
    ExpressionPrecedence6,
};

ExpressionPrecedence6: Expression = {
    <lhs:ExpressionPrecedence6> "==" <rhs:ExpressionPrecedence5> => Expression::BinaryOp{op: BinaryOp::EQ, lhs: Box::new(lhs), rhs: Box::new(rhs)},
    <lhs:ExpressionPrecedence6> "!=" <rhs:ExpressionPrecedence5> => Expression::BinaryOp{op: BinaryOp::NE, lhs: Box::new(lhs), rhs: Box::new(rhs)},
    <lhs:ExpressionPrecedence6> "<" <rhs:ExpressionPrecedence5> => Expression::BinaryOp{op: BinaryOp::LT, lhs: Box::new(lhs), rhs: Box::new(rhs)},
    ExpressionPrecedence5
}

// ArithmeticExpression
ExpressionPrecedence5: Expression = {
    <lhs:ExpressionPrecedence5> "+" <rhs:ExpressionPrecedence4> => Expression::BinaryOp{op: BinaryOp::Sum, lhs: Box::new(lhs), rhs: Box::new(rhs)},
    <lhs:ExpressionPrecedence5> "-" <rhs:ExpressionPrecedence4> => Expression::BinaryOp{op: BinaryOp::Sub, lhs: Box::new(lhs), rhs: Box::new(rhs)},
    ExpressionPrecedence4
}

// FactorExpression
ExpressionPrecedence4: Expression = {
    <lhs:ExpressionPrecedence4> "*" <rhs:ExpressionPrecedence3> => Expression::BinaryOp{op: BinaryOp::Mul, lhs: Box::new(lhs), rhs: Box::new(rhs)},
    <lhs:ExpressionPrecedence4> "/" <rhs:ExpressionPrecedence3> => Expression::BinaryOp{op: BinaryOp::Div, lhs: Box::new(lhs), rhs: Box::new(rhs)},
    ExpressionPrecedence3
};

ExpressionPrecedence3: Expression = {
    ExpressionPrecedence2
}

ExpressionPrecedence2: Expression = {
    "NOT" <expression:ExpressionPrecedence2> => Expression::UnaryOp{op: UnaryOp::Not, expr: Box::new(expression)},
    "'" <expression:ExpressionPrecedence2> => Expression::UnaryOp{op: UnaryOp::Dereference, expr: Box::new(expression)},
    ExpressionPrecedence1
}

ExpressionPrecedence1: Expression = {
    LiteralExpression,
    FunctionCall,
    Variable,
    "(" <Expression> ")",

};

LiteralExpression: Expression = {
    BoolLiteral,
    IntLiteral,
    StringLiteral,
    FloatLiteral,
    NullLiteral
};

FunctionCall: Expression = {
    <function: "IDENTIFIER"> "{" <args: Parameters> "}" => Expression::Call{function, args: args.into_iter().map(Box::new).collect()},
}

#[inline]
Comma<T>: Vec<T> = { // (1)
    <mut v:(<T> ",")*> <e:T?> => match e { // (2)
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

#[inline]
Semicolon<T>: Vec<T> = { // (1)
    <mut v:(<T> ";")*> <e:T?> => match e { // (2)
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};
#[inline]
Statements: Statements = {
    <statements: Semicolon<SimpleStatement>> => Statements::SimpleStatements(statements),
    <statement:OneLineStatement> => Statements::OneLineStatement(statement)
};

#[inline]
SimpleStatements = Semicolon<SimpleStatement>;

#[inline]
Parameters = Comma<Expression>;

Variable: Expression = {
    <name:"IDENTIFIER"> => Expression::Var{name}
}

NullLiteral: Expression = {
    <n:"NULL"> => Expression::Null,
};


IntLiteral: Expression = {
    <n:"INTEGER_LITERAL"> => Expression::Int{value: n},
};

FloatLiteral: Expression = {
    <n:"FLOAT_LITERAL"> => Expression::Float{value: n},
}

BoolLiteral: Expression = {
    "TRUE" => Expression::Bool{value: true},
    "FALSE" => Expression::Bool{value: false},

};

StringLiteral: Expression = {
    <s: "STRING_LITERAL"> => Expression::String{value: s}
};

Identifier: String = {
    <i: "IDENTIFIER"> => i
};

extern {
    type Location = LexLocation;
    type Error = LexError;

    enum Token {
    "IDENTIFIER" => Token::Identifier (<String>),
    "INTEGER_LITERAL" => Token::IntegerLiteral (<i64>),
    "FLOAT_LITERAL" => Token::FloatLiteral (<f64>),
    "STRING_LITERAL" => Token::StringLiteral (<String>),
    "NEW_LINE" => Token::NewLine,
    "END_OF_FILE" => Token::EndOfFile,
    "(" => Token::LeftParenthesis,
        ")" => Token::RightParenthesis,
        "[" => Token::LeftSquareBracket,
        "]" => Token::RightSquareBracket,
        ":" => Token::Colon,
        "," => Token::Comma,
        ";" => Token::Semicolon,
        "*" => Token::Multiply,
        "+" => Token::Plus,
        "-" => Token::Minus,
        "/" => Token::Slash,
        "|" => Token::VerticalBar, // '|'
        "&" => Token::Ampersand,    // '&'
        "<" => Token::LessThan,
        ">" => Token::GreaterThan,
        "=" => Token::Equal,
        "." => Token::Dot,
        "%" => Token::Percent,
        "{" => Token::LeftCurlyBrace,
        "}" => Token::RightCurlyBrace,
        "==" => Token::EqualEqual,
        "!=" => Token::NotEqual,
        "=<" => Token::LessThanEqual,
        ">=" => Token::GreaterThanEqual,
        ">>" => Token::RightShift,
        "<<" => Token::LeftShift,
        "//" => Token::DoubleSlash, // '//'
        "=>" => Token::Send,
        "..." => Token::Ellipsis,
        "'" => Token::Apostrophe,
        "<=>" => Token::Exchange,
        "REPLACE" => Token::Replace,
        "LOOP" => Token::Loop,
        "PREDICATE" => Token::Predicate,
        "@" => Token::At,
        "!" => Token::Bang,

        // Basic Keywords:
        "FALSE" => Token::False,
        "NULL" => Token::Null,
        "TRUE" => Token::True,
        "AND" => Token::And,
        "DEL" => Token::Del,
        "NOT" => Token::Not,
        "OR" => Token::Or,
        "LET" => Token::Let,
        "CONST" => Token::Const,
    }
}